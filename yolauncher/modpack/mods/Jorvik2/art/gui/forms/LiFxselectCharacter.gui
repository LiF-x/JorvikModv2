// base for all buttons
singleton GuiControlProfile( CharSelectButtonProfile : GuiBaseTextProfile) {
	globalImageIndex = "atlas2";
	fontSize = 20;
	justify = "center";
	textOffset = "0 -2";
	border = "none";
	fontColor = "204 204 204";
	fontColorHL = "204 204 204";
	fontColorNA = "204 204 204";
	fontColorSEL = "204 204 204";
};
// Play button
singleton GuiControlProfile( CharSelectButtonPlayProfile : CharSelectButtonProfile) {
	fontSize = 32;
};
// Create button
singleton GuiControlProfile( CharSelectButtonCreateProfile : CharSelectButtonProfile) {
	fontSize = 32;
};
// Menu & Word selection buttons
singleton GuiControlProfile( CharSelectButtonMenuProfile : CharSelectButtonProfile) {
	fontSize = 24;
};
// Character button
singleton GuiControlProfile( CharSelectButtonCharProfile : CharSelectButtonProfile) {
	fontSize = 24;
	textOffset = "19 -2";
	justify = "left";
};

singleton GuiControlProfile( CharSelectLabel32LeftHeadProfile : GuiBaseCaptionProfile ) {
	fontSize = 36;
	justify = "left";
	fontColor = "255 255 255";
};

singleton GuiControlProfile( CharSelectStatLabelProfile : GuiBaseTextProfile ) {
	fontSize = 24;
	justify = "left";
	fontColor = "204 204 204";
};
singleton GuiControlProfile( CharSelectStatValueProfile : CharSelectStatLabelProfile ) {
	justify = "right";
};
singleton GuiControlProfile( CharSelectAlignmentValueProfile : CharSelectStatValueProfile ) {};

singleton GuiControlProfile( CharSelectCharNameProfile : GuiBaseCaptionProfile ) {
	fontSize = 37;
	justify = "left";
	fontColor = "178 128 28";
};

singleton GuiControlProfile( CharSelectCharRaceProfile : GuiBaseCaptionProfile ) {
	fontSize = 28;
	justify = "center";
	fontColor = "153 153 153";
};

singleton GuiControlProfile( CharSelectGuildRankLabelProfile : GuiBaseTextProfile) {
	fontSize = 20;
	justify = "left";
	fontColor = "137 137 137";
};
singleton GuiControlProfile( CharSelectGuildRankValueProfile : GuiBaseTextProfile) {
	fontSize = 22;
	justify = "left";
	fontColor = "204 204 204";
};

singleton GuiControlProfile( EmptynessProfile : GuiBaseTextProfile) {
	opaque = true;
	fillcolor = "0 0 0";
};

%guiContent = new GuiControl( selectCharacterDlg) {
	position = "0 0";
	extent = "1024 720";
	minExtent = "8 2";
	horizSizing = "right";
	vertSizing = "bottom";
	profile = "EmptynessProfile";
	visible = "1";
	active = "1";

	new GuiBitmapCtrl() {		// top line
		bitmap = "gui/images/charselect_header.png";
		horizSizing = "width";
		vertSizing = "bottom";
		position = "0 0";
		extent = "1919 82";
		visible = "1";
		profile = "GuiContentProfile";
		wrap = true;
	};

	new GuiControl() {
		position = "0 0";
		extent = "1024 720";
		minExtent = "1024 720";
		horizSizing = "width";
		vertSizing = "height";
		profile = "GuiDefaultProfile";
		visible = "1";
		active = "1";

		new GuiTextCtrl() {		// text: Your characters
			text = GetMessageIDText(2196);
			horizSizing = "width";
			vertSizing = "bottom";
			position = "30 24";
			extent = "600 30";
			profile = "CharSelectLabel32LeftHeadProfile";
			canHit = "false";
			visible = "1";
			internalName = "yourCharacters";
		};

		new guiHorzTilingButtonCtrl() {
			internalName = "worldSelection";
			text = GetMessageIDText(2521); // World selection
			position = "544 18";
			extent = "286 44";
			minExtent = "286 44";
			horizSizing = "left";
			vertSizing = "bottom";
			profile = CharSelectButtonMenuProfile;
			groupNum = "-1";
			buttonType = "PushButton";
			useMouseEvents = "0";
			visible = "0";
			active = "1";
			renderText = "1";
			command = "_requestWorldSelection();";
			imageIndex = "H44Btn";
		};//button

		new guiHorzTilingButtonCtrl() {
			text = GetMessageIDText(2495); // Menu
			position = "846 18";
			extent = "158 44";
			horizSizing = "left";
			vertSizing = "bottom";
			profile = "CharSelectButtonMenuProfile";
			groupNum = "-1";
			buttonType = "PushButton";
			useMouseEvents = "0";
			visible = "1";
			active = "1";
			imageIndex = getBtnRandomEmblem();
			command = "showEscMenu();";
			accelerator = "escape";
			imageIndex = "H44Btn";
		};//button

		new GuiControl( SelectCharacterList ) { // left, characters
			horizSizing = "relative";
			vertSizing = "height";
			position = "0 84";
			extent = "386 636";
			minExtent = "386 612";
			maxExtent = "690 10000";
			profile = "GuiDefaultProfile";
			visible = "1";

			internalName = "charList";

			new guiHorzTilingButtonCtrl(CreatePlayerButton) {
				text = GetMessageIDText(1231); // Create a new character
				horizSizing = "width";
				vertSizing = "top";
				position = "30 561";
				extent = "325 56";
				minExtent = "8 2";
				profile = "CharSelectButtonCreateProfile";
				groupNum = "-1";
				buttonType = "PushButton";
				useMouseEvents = "0";
				visible = "1";
				active = "1";
				command = "selectCharacterDlg::onCreatePlayerButClick();";
				isContainer = "0";
				canSave = "1";
				canSaveDynamicFields = "0";
				imageIndex = "H56Btn";
			};

		};//control

		new GuiControl() { // right, character info
			horizSizing = "relative";
			vertSizing = "height";
			position = "386 84";
			extent = "638 519";
			minExtent = "638 519";
			profile = "GuiBaseTextProfile";
			visible = "1";

			new GuiBitmapCtrl() {
				// the sizing & positioning is driven by the class
				extent = "1920 1080";
				minExtent = "0 0";
				position = "-1282 -561";
				horizSizing = "width";
				vertSizing = "height";
				class = GuiCharSelectOrnamentBGClass;
				bitmap = "gui/images/Main_back_ornament.png";
				profile = "GuiContentProfile";
			};

			new GuiScrollCtrl( CharInfoPanel ) {
				horizSizing = "width";
				vertSizing = "height";
				position = "0 10";
				extent = "640 500";
				minExtent = "16 16";
				willFirstRespond = "1";
				hScrollBar = "alwaysOff";
				vScrollBar = "dynamic";
				lockHorizScroll = "0";
				lockVertScroll = "0";
				constantThumbHeight = "0";
				childMargin = "0 0";
				mouseWheelScrollSpeed = "-1";
				margin = "0 0 0 0";
				padding = "0 0 0 0";
				anchorTop = "1";
				anchorBottom = "0";
				anchorLeft = "1";
				anchorRight = "0";
				profile = "GuiEagleScrollBarProfile";
				visible = "0";
				active = "1";
				isContainer = "1";
				canSave = "1";
				canSaveDynamicFields = "0";
				internalName = "charInfo";
				constantThumbHeight = true;
				trackOffset = 12;
				arrowSadowSize = 2;
				addContentWidth = 0;

				// for some reason without this the next control is placed at 0,0
				new GuiControl() {};

				new GuiControl() { // Race
					horizSizing = "right";
					vertSizing = "bottom";
					position = "30 49";
					extent = "200 200";
					visible = "true";

					new GuiBitmapCtrl() { // Race background
						position = "0 0";
						Extent = "200 200";
						Visible = "1";
						profile = "GuiAtlas3ImageProfile";
						imageIndex = "CharSelectRacePnl";
					};
					new GuiBitmapCtrl() { // Race icon
						position = "45 20";
						Extent = "110 110";
						Visible = "1";
						profile = "GuiAtlas3ImageProfile";
						internalName = "raceIcon";
					};
					new GuiTextCtrl() { // Race text
						position = "24 126";
						extent = "152 41";
						profile = "CharSelectCharRaceProfile";
						canHit = "false";
						visible = "1";
						internalName = "raceName";
					};
				};

				new GuiTextCtrl() { // Char Name
					text = "";
					position = "250 38";
					extent = "426 30";
					profile = "CharSelectCharNameProfile";
					canHit = "false";
					visible = "1";
					internalName = "charName";
				};

				new GuiControl() { // Guild Rank background
					horizSizing = "width";
					vertSizing = "bottom";
					position = "214 93";
					extent = "360 52";
					minExtent = "360 52";
					maxExtent = "500 52";
					Visible = "1";
					profile = "GuiAtlas3TiledImageProfile";

					leftIndex0 = "BigHexagonalTextFieldLeft";
					backgroundIndex = "BigHexagonalTextFieldCenter";
					rightIndex0 = "BigHexagonalTextFieldRight";

					new GuiTextCtrl() { // Guild Info label
						text = GetMessageIDText(2194) @ "," SPC GetMessageIDText(2195);
						position = "32 14";
						extent = "296 24";
						profile = "CharSelectGuildRankLabelProfile";
						canHit = "false";
						visible = "1";

						internalName = "guildInfoLabel";
					};
					new GuiTextCtrl() { // Guild Info
						horizSizing = "width";
						position = "32 14";
						extent = "296 24";
						profile = "CharSelectGuildRankValueProfile";
						canHit = "false";
						visible = "0";

						internalName = "guildInfo";
					};
				};// Guild Rank background

				new GuiControl() {				// Title background
					horizSizing = "width";
					vertSizing = "bottom";
					position = "214 152";
					extent = "360 52";
					minExtent = "360 52";
					maxExtent = "500 52";
					Visible = "1";
					profile = "GuiAtlas3TiledImageProfile";

					leftIndex0 = "BigHexagonalTextFieldLeft";
					backgroundIndex = "BigHexagonalTextFieldCenter";
					rightIndex0 = "BigHexagonalTextFieldRight";

					new GuiTextCtrl() { // Title label
						text = GetMessageIDText(1299);
						position = "32 14";
						extent = "296 24";
						profile = "CharSelectGuildRankLabelProfile";
						canHit = "false";
						visible = "1";

						internalName = "charTitleLabel";
					};
					new GuiTextCtrl() { // Title
						horizSizing = "width";
						position = "32 14";
						extent = "296 24";
						profile = "CharSelectGuildRankValueProfile";
						canHit = "false";
						visible = "1";

						internalName = "charTitle";
					};
				};// Title background

				new GuiTextCtrl() { // States label
					text = GetMessageIDText(2197); // States of personality
					position = "250 232";
					extent = "408 30";
					profile = "CharSelectCharNameProfile";
					canHit = "false";
					visible = "1";
				};
				//------------------------------------------------------
			};//GuiScrollCtrl( CharInfoPanel)
		};//control

		new GuiBitmapCtrl() { // bottom right panel
			horizSizing = "relative";
			vertSizing = "top";
			position = "386 604";
			extent = "638 117";
			minExtent = "638 117";
			visible = "1";
			profile = "GuiAtlas3ImageProfile";
			wrap = true;

			imageIndex = "LearningWindowBackgroundTransparent";
		};

		new guiHorzTilingButtonCtrl(RulesBut) {
			text = GetMessageIDText(5207); // Server Rules
			position = "175 617";
			extent = "398 84";
			minExtent = "398 84";
			horizSizing = "left";
			vertSizing = "top";
			profile = "CharSelectButtonPlayProfile";
			groupNum = "-1";
			buttonType = "PushButton";
			useMouseEvents = "0";
			visible = "1";
			active = "1";
			command = "displayRules(true);";
			isContainer = "0";
			canSave = "1";
			canSaveDynamicFields = "0";
			imageIndex = "H84Btn";
		};

		new guiHorzTilingButtonCtrl(PlayBut) {
			text = GetMessageIDText(1232); // Play
			position = "605 617";
			extent = "398 84";
			minExtent = "398 84";
			horizSizing = "left";
			vertSizing = "top";
			profile = "CharSelectButtonPlayProfile";
			groupNum = "-1";
			buttonType = "PushButton";
			useMouseEvents = "0";
			visible = "1";
			active = "1";
			command = "CharSelectionPlayPressed();";
			isContainer = "0";
			canSave = "1";
			canSaveDynamicFields = "0";
			imageIndex = "H84Btn";
		};

	};//GuiControl

	new GuiControl() { // graphical thingy
		horizSizing = "relative";
		vertSizing = "height";
		extent = "36 1080";
		position = "386 83";
		visible = "1";

		new GuiBitmapCtrl() {
			vertSizing = "height";
			position = "0 0";
			extent = "36 1080";
			profile = "GuiAtlas1ImageProfile";
			wrap = true;

			imageIndex = "FringeLeftShadow";
		};
	};

	new GuiControl() { // 3px line under topline
		horizSizing = "width";
		vertSizing = "bottom";
		position = "0 81";
		extent = "1024 3";
		visible = "1";
		profile = "GuiAtlas3TiledImageProfile";

		leftIndex0 = "BlackSlimHLineLeft";
		backgroundIndex = "BlackSlimHLineCenter";
		rightIndex0 = "BlackSlimHLineRight";
	};

	new GuiControl() { // 3px line between stats & bottom plate
		horizSizing = "relative";
		vertSizing = "top";
		position = "386 602";
		extent = "638 3";
		minExtent = "638 3";
		visible = "1";
		profile = "GuiAtlas3TiledImageProfile";

		leftIndex0 = "BlackSlimHLineLeft";
		backgroundIndex = "BlackSlimHLineCenter";
		rightIndex0 = "BlackSlimHLineRight";
	};

};//GuiControl

// creates a single character slot button
function createCharSlotButton(%internalName)
{
	%button = new guiHorzTilingButtonCtrl() {
		horizSizing = "width";
		vertSizing = "bottom";
		extent = "325 44";
		visible = "true";
		active = "true";
		profile = "CharSelectButtonCharProfile";
		buttonType = "RadioButton";
		textMargin = "0 0 0 35";
		renderDotIfTextDoesNotFit = true;

		useManualIndex = "true";
		imageIndex = "H44BtnDisabled";
		depressedImageIndex = "H44Btn";
		mouseOverImageIndex = "H44BtnMouseOver";

		internalName = %internalName;

		new GuiButtonCtrl() {
			horizSizing = "left";
			vertSizing = "bottom";
			extent = "17 17";
			position = "295 13";
			buttonType = "RadioButton";
			profile = "GuiAtlas3ImageProfile";

			imageIndex = "Cross";
			internalName = "charDelButton";
		};
	};
	return %button;
}

// creates a stat panel frame
function createStatFrame(%handle, %nameId, %icon)
{
	%frame = new GuiControl() {
		horizSizing = "width";
		vertSizing = "bottom";
		extent = "360 62";
		minExtent = "360 62";
		maxExtent = "408 128";
		profile = "GuiDefaultProfile";
		visible = "1";
		active = "1";

		internalName = %handle @ "Frame";

		new GuiControl() { // Background
			horizSizing = "width";
			position = "1 9";
			extent = "359 44";
			visible = "1";
			profile = "GuiAtlas3TiledImageProfile";

			leftIndex0 = "CharSelectParamPnlLeft";
			backgroundIndex = "CharSelectParamPnlCenter";
			rightIndex0 = "CharSelectParamPnlRight";

			internalName = %handle @ "Background";

			new GuiTextCtrl() { // Label
				text = GetMessageIDText(%nameId);
				position = "78 0";
				extent = "329 42";
				profile = "CharSelectStatLabelProfile";
				canHit = "false";
				visible = "1";

				internalName = %handle @ "Label";
			};
			new GuiTextCtrl() { // Value
				text = "0";
				horizSizing = "left";
				position = "272 0";
				extent = "51 42";
				profile = "CharSelectStatValueProfile";
				canHit = "false";
				visible = "1";

				internalName = %handle @ "Value";
			};
		};

		new GuiBitmapCtrl() { // Icon
			position = "0 0";
			extent = "62 62";
			visible = "1";
			profile = "GuiAtlas3ImageProfile";
			imageIndex = %icon;

			internalName = %handle @ "Icon";
		};
	};
	return %frame;
}

// creating the 10 character slot buttons
for ( %i = 0; %i < CharSelectionGetMaxCharSlots(); %i++ )
{
	%button = createCharSlotButton("charSlot" @ %i);
	%guiContent-->charList.add(%button);

	%button.setPosition(30, 20 + (%button.extent.y + 10) * %i);
	%button.command = "CharSelectionSlotPressed(" @ %i @ ");";
	%button-->charDelButton.command = "CharSelectionDelPressed(" @ %i @ ");";
}

// creating stat frames
%statFrames[0] = createStatFrame("stat0", 1091, "CharSelectStrengthIcon");
%statFrames[1] = createStatFrame("stat1", 1092, "CharSelectAgilityIcon");
%statFrames[2] = createStatFrame("stat2", 1093, "CharSelectConstitutionIcon");
%statFrames[3] = createStatFrame("stat3", 1095, "CharSelectIntellectIcon");
%statFrames[4] = createStatFrame("stat4", 1094, "CharSelectWillpowerIcon");
%statFrames[5] = createStatFrame("stat5", 1097, "CharSelectLuckIcon");

for ( %i = 0; %i < 6; %i++ )
{
	%statFrame = %statFrames[%i];
	%guiContent-->charInfo.add(%statFrame);

	%statFrame.setPosition(214, 280 + (%statFrame.extent.y + 4) * %i);
}

// special case for the alignment
%alignmentFrame = createStatFrame("alignment", 1100, "PositiveAlignmentBigIcon");
%guiContent-->charInfo.add(%alignmentFrame);

%alignmentFrame.extent = "426 128";
%alignmentFrame.minExtent = "426 128";
%alignmentFrame.maxExtent = "474 128";
%alignmentFrame-->alignmentBackground.setPosition(67, 42);
%alignmentFrame-->alignmentBackground.extent.x -= 66;
%alignmentFrame-->alignmentValue.profile = CharSelectAlignmentValueProfile;
%alignmentFrame-->alignmentIcon.setExtent(128, 128);
%alignmentFrame.setPosition(148, %statFrames[5].position.y + %statFrames[5].extent.y + 4);

//----------------------------------------

function selectCharacterDlg::onWake(%this)
{
   CreatePlayerButton.setVisible($cm_config::AllowCharacterCreation);
   // %this-->worldSelection.setVisible(!$cmYO);
   if (!$cmYO)
   {
      %description = $worldDescription[$cm_config::GlobalWorldId];
      if (%description $= "")
         %this-->yourCharacters.setText(GetMessageIDText(2196)); // Your characters
      else
         %this-->yourCharacters.setText(GetMessageIDText(3139, %description)); // Your characters on %1
   }
   if(%this.needCreateChar)
   {
      selectCharacterDlg.schedule(0, onCreatePlayerButClick);
      %this.needCreateChar = false;
   }
}

function selectCharacterDlg::onCreatePlayerButClick()
{
   // load managers before open character customization window
   closeCharacterSelectionDialog();
   initManagersOnCreatePlayer();
}

function GuiCharSelectOrnamentBGClass::onThisControlResized(%this)
{
	if (%this.resizing)
		return;

	%minWidth = 1920;
	%minHeight = 1080;
	%extent = %this.getExtent();
	%width = %extent.x;
	%height = %extent.y;

	// calculate new size
	%aspectRatio = %minWidth / %minHeight;
	if (%aspectRatio > (%width / %height))
	{
		%height = getMax(%height, %minHeight);
		%width = mFloor(%height * %aspectRatio);
	}
	else
	{
		%width = getMax(%width, %minWidth);
		%height = mFloor(%width / %aspectRatio);
	}

	// calculate new position
	%parentExtent = %this.getParent().getExtent();
	%posX = %parentExtent.x - %width;
	%posY = %parentExtent.y - %height;

	%this.resizing = true;
	%this.resize(%posX, %posY, %width, %height);
	%this.resizing = false;
}

function initManagersOnCreatePlayer()
{
   %list = new ArrayObject();
   //%list.push_back("CmSoundsManager");
   %list.push_back("cBodyParts");

   initManagersListThreaded(%list, "CharSelectionCreatePressed();");
   %list.delete();

   loadLoadingGui(GetMessageIDText(2141)); // Initializing
}

function showCharacterSelectionDialog()
{
   info(selectCharacterDlg.needCreateChar);
	Canvas.pushDialog(selectCharacterDlg);
}

function hideCharacterSelectionDialog()
{
   info(selectCharacterDlg.needCreateChar);
	Canvas.popDialog(selectCharacterDlg);
}
